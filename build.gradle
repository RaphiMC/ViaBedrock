plugins {
    id "java-library"
    id "maven-publish"
    id "net.kyori.blossom" version "1.3.1"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
compileJava.options.encoding = compileTestJava.options.encoding = javadoc.options.encoding = "UTF-8"

group = project.maven_group
archivesBaseName = project.maven_name
version = project.maven_version

configurations {
    testImplementation.extendsFrom compileOnly
}

repositories {
    mavenCentral()
    maven {
        name = "ViaVersion"
        url "https://repo.viaversion.com"
    }
}

dependencies {
    compileOnly "com.viaversion:viaversion:4.7.0"
    compileOnly "org.yaml:snakeyaml:2.0"
    compileOnly "com.google.guava:guava:32.0.1-jre"
    compileOnly "io.netty:netty-handler:4.1.93.Final"
    compileOnly "com.google.code.gson:gson:2.10.1"

    api("io.netty:netty-codec-http:4.1.93.Final") {
        transitive = false
    }
    api "io.jsonwebtoken:jjwt-impl:0.11.5"
    api("io.jsonwebtoken:jjwt-gson:0.11.5") {
        exclude group: "com.google.code.gson", module: "gson"
    }
    api("net.lenni0451.mcstructs-bedrock:text:1.1.0") {
        exclude group: "com.google.code.gson", module: "gson"
    }
    api("net.lenni0451.mcstructs:text:2.2.6") {
        exclude group: "com.google.code.gson", module: "gson"
    }
    api "com.vdurmont:semver4j:3.1.0"
}

blossom {
    replaceToken("\${version}", project.version, "src/main/java/net/raphimc/viabedrock/ViaBedrock.java")
}

java {
    withSourcesJar()
    withJavadocJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    repositories {
        maven {
            name = "reposilite"
            url = "https://maven.lenni0451.net/snapshots"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = project.maven_group
            artifactId = project.maven_name
            version = project.maven_version

            from components.java
        }
    }
}
