plugins {
    id "java-library"
    id "base.base-conventions"
    id "base.fill-build-constants"
    id "viabedrock.publishing-conventions"
    id "via.run-with-viaproxy-task"
    id "net.raphimc.class-token-replacer" version "1.1.7"
}

sourceSets {
    tool

    tool.compileClasspath += main.output + main.compileClasspath
    tool.runtimeClasspath += tool.compileClasspath
}

repositories {
    maven {
        name = "ViaVersion"
        url = "https://repo.viaversion.com"

        content {
            includeGroupByRegex "com\\.viaversion(\\..+)?"
            includeGroupByRegex "net\\.raphimc(\\..+)?"
        }
    }
    maven {
        name = "Minecraft Libraries"
        url = "https://libraries.minecraft.net"

        content {
            includeGroup "com.mojang"
        }
    }
    maven {
        name = "Jitpack"
        url = "https://jitpack.io"

        content {
            includeGroupByRegex "com\\.github\\..+"
        }
    }
}

dependencies {
    compileOnly "com.viaversion:viaversion-common:5.6.1-SNAPSHOT"
    compileOnly "org.yaml:snakeyaml:2.5"
    compileOnly "com.google.guava:guava:33.5.0-jre"
    compileOnly "io.netty:netty-handler:4.2.9.Final"
    compileOnly "com.google.code.gson:gson:2.13.2"

    api("io.netty:netty-codec-http:4.2.9.Final") {
        transitive = false
    }
    api("io.netty:netty-codec-compression:4.2.9.Final") {
        transitive = false
    }
    api "io.jsonwebtoken:jjwt-impl:0.13.0"
    api("io.jsonwebtoken:jjwt-gson:0.13.0") {
        exclude group: "com.google.code.gson", module: "gson"
    }
    api("net.lenni0451.mcstructs-bedrock:text:2.0.1") {
        exclude group: "com.google.code.gson", module: "gson"
    }
    api("net.lenni0451.mcstructs-bedrock:forms:2.0.1") {
        exclude group: "com.google.code.gson", module: "gson"
    }
    api("com.viaversion.mcstructs:dialog:5-3.2.2-20251210.004302-1") {
        exclude group: "com.viaversion.mcstructs", module: "core"
        exclude group: "com.viaversion.mcstructs", module: "text"
        exclude group: "com.viaversion.mcstructs", module: "snbt"
        exclude group: "com.viaversion.mcstructs", module: "converter"
    }
    api "com.vdurmont:semver4j:3.1.0"
    api "com.mojang:brigadier:1.3.10"
    api "at.yawk.lz4:lz4-java:1.10.2"
    api("com.github.oryxel1:CubeConverter:5b4fce77c4") {
        transitive = false
    }
    api("team.unnamed:mocha:3.0.1") {
        transitive = false
    }

    toolImplementation "org.jsoup:jsoup:1.21.2"
    toolImplementation "org.ow2.asm:asm-tree:9.9.1"
}
