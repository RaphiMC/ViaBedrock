plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
    id "net.kyori.blossom" version "1.3.1"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
compileJava.options.encoding = compileTestJava.options.encoding = javadoc.options.encoding = "UTF-8"

group = project.maven_group
archivesBaseName = project.maven_name
version = project.maven_version

configurations {
    testImplementation.extendsFrom compileOnly
}

repositories {
    mavenCentral()
    maven {
        name = "ViaVersion"
        url "https://repo.viaversion.com"
    }
    maven {
        name = "Lenni0451 Snapshots"
        url "https://maven.lenni0451.net/snapshots"
    }
    maven {
        name = "Minecraft Libraries"
        url "https://libraries.minecraft.net"
    }
}

dependencies {
    compileOnly "com.viaversion:viaversion:4.7.0"
    compileOnly "org.yaml:snakeyaml:2.1"
    compileOnly "com.google.guava:guava:32.1.2-jre"
    compileOnly "io.netty:netty-handler:4.1.96.Final"
    compileOnly "com.google.code.gson:gson:2.10.1"

    api("io.netty:netty-codec-http:4.1.96.Final") {
        transitive = false
    }
    api "io.jsonwebtoken:jjwt-impl:0.11.5"
    api("io.jsonwebtoken:jjwt-gson:0.11.5") {
        exclude group: "com.google.code.gson", module: "gson"
    }
    api("net.lenni0451.mcstructs-bedrock:text:1.2.0-SNAPSHOT") {
        exclude group: "com.google.code.gson", module: "gson"
    }
    api("net.lenni0451.mcstructs-bedrock:forms:1.2.0-SNAPSHOT") {
        exclude group: "com.google.code.gson", module: "gson"
    }
    api("net.lenni0451.mcstructs:text:2.3.1") {
        exclude group: "com.google.code.gson", module: "gson"
    }
    api "com.vdurmont:semver4j:3.1.0"
    api "com.mojang:brigadier:1.0.18"
}

blossom {
    replaceToken("\${version}", project.version, "src/main/java/net/raphimc/viabedrock/ViaBedrock.java")
}

java {
    withSourcesJar()
    withJavadocJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    repositories {
        maven {
            name = "reposilite"
            url = project.maven_version.endsWith("SNAPSHOT") ? "https://maven.lenni0451.net/snapshots" : "https://maven.lenni0451.net/releases"

            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = project.maven_group
            artifactId = project.maven_name
            version = project.maven_version

            from components.java

            pom {
                name = "ViaBedrock"
                description = "ViaVersion addon to add support for Minecraft: Bedrock Edition servers"
                url = "https://github.com/RaphiMC/ViaBedrock"
                licenses {
                    license {
                        name = "GPL-3.0 License"
                        url = "https://github.com/RaphiMC/ViaBedrock/blob/main/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "RK_01"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/RaphiMC/ViaBedrock.git"
                    developerConnection = "scm:git:ssh://github.com/RaphiMC/ViaBedrock.git"
                    url = "https://github.com/RaphiMC/ViaBedrock.git"
                }
            }
        }
    }
}

signing {
    setRequired(false)
    sign configurations.archives
    sign publishing.publications.maven
}

project.tasks.withType(PublishToMavenRepository).forEach {
    it.dependsOn(project.tasks.withType(Sign))
}
