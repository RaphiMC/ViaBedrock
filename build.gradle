plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
    id "idea"
    id "net.raphimc.class-token-replacer" version "1.0.0"
}

base {
    java.toolchain.languageVersion = JavaLanguageVersion.of(8)
    compileJava.options.encoding = compileTestJava.options.encoding = javadoc.options.encoding = "UTF-8"

    group = project.maven_group ?: rootProject.maven_group
    archivesName = project.maven_name ?: rootProject.maven_name
    version = project.maven_version ?: rootProject.maven_version
}

configurations {
    testImplementation.extendsFrom compileOnly
}

repositories {
    mavenCentral()
    maven {
        name = "ViaVersion"
        url "https://repo.viaversion.com"
    }
    maven {
        name = "Lenni0451"
        url "https://maven.lenni0451.net/everything"
    }
    maven {
        name = "Minecraft Libraries"
        url "https://libraries.minecraft.net"
    }
}

dependencies {
    compileOnly "com.viaversion:viaversion-common:4.10.0-24w09a-SNAPSHOT"
    compileOnly "org.yaml:snakeyaml:2.2"
    compileOnly "com.google.guava:guava:33.1.0-jre"
    compileOnly "io.netty:netty-handler:4.1.107.Final"
    compileOnly "com.google.code.gson:gson:2.10.1"

    api("io.netty:netty-codec-http:4.1.108.Final") {
        transitive = false
    }
    api "io.jsonwebtoken:jjwt-impl:0.12.5"
    api("io.jsonwebtoken:jjwt-gson:0.12.5") {
        exclude group: "com.google.code.gson", module: "gson"
    }
    api("net.lenni0451.mcstructs-bedrock:text:1.2.0") {
        exclude group: "com.google.code.gson", module: "gson"
    }
    api("net.lenni0451.mcstructs-bedrock:forms:1.2.0") {
        exclude group: "com.google.code.gson", module: "gson"
    }
    api "com.vdurmont:semver4j:3.1.0"
    api "com.mojang:brigadier:1.0.18"
    api("org.iq80.leveldb:leveldb:0.12") {
        exclude group: "com.google.guava"
    }
    api "org.lz4:lz4-pure-java:1.8.0"
}

sourceSets {
    main {
        classTokenReplacer {
            property("\${version}", project.version)
            property("\${impl_version}", "git-${project.name}-${project.version}:${project.latestCommitHash()}")
        }
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.name ?: rootProject.name}" }
    }
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    repositories {
        maven {
            name = "reposilite"
            url = "https://maven.lenni0451.net/" + (project.maven_version.endsWith("SNAPSHOT") ? "snapshots" : "releases")

            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = project.maven_group
            artifactId = project.maven_name
            version = project.maven_version

            from components.java

            pom {
                name = "ViaBedrock"
                description = "ViaVersion addon to add support for Minecraft: Bedrock Edition servers"
                url = "https://github.com/RaphiMC/ViaBedrock"
                licenses {
                    license {
                        name = "GPL-3.0 License"
                        url = "https://github.com/RaphiMC/ViaBedrock/blob/main/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "RK_01"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/RaphiMC/ViaBedrock.git"
                    developerConnection = "scm:git:ssh://github.com/RaphiMC/ViaBedrock.git"
                    url = "https://github.com/RaphiMC/ViaBedrock.git"
                }
            }
        }
    }
}

signing {
    setRequired(false)
    sign configurations.archives
    sign publishing.publications.maven
}

project.tasks.withType(PublishToMavenRepository).forEach {
    it.dependsOn(project.tasks.withType(Sign))
}

idea {
    module {
        ["run"].each {
            excludeDirs << file("$it")
        }
    }
}

String latestCommitHash() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine "git", "rev-parse", "--short", "HEAD"
        standardOutput = stdout
    }
    return stdout.toString().trim()
}
