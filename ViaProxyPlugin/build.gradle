plugins {
    id "java-library"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
compileJava.options.encoding = compileTestJava.options.encoding = javadoc.options.encoding = "UTF-8"

group = project.maven_group
archivesBaseName = "ViaProxy-ViaBedrockPlugin"
version = project.maven_version

configurations {
    include

    implementation.extendsFrom include
    api.extendsFrom include
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = "Jitpack"
        url = "https://jitpack.io"
    }
    maven {
        name = "Lenni0451"
        url "https://maven.lenni0451.net/releases"
    }
    maven {
        name = "Lenni0451"
        url "https://maven.lenni0451.net/snapshots"
    }
    maven {
        name = "OpenCollab Releases"
        url = "https://repo.opencollab.dev/maven-releases/"
    }
    maven {
        name = "OpenCollab Snapshots"
        url = "https://repo.opencollab.dev/maven-snapshots/"
    }
}

dependencies {
    compileOnly "net.raphimc:ViaProxy:3.0.14"

    include("org.cloudburstmc.netty:netty-transport-raknet:0.0.1.Final-SNAPSHOT") {
        exclude group: "io.netty", module: "netty-common"
        exclude group: "io.netty", module: "netty-buffer"
        exclude group: "io.netty", module: "netty-codec"
        exclude group: "io.netty", module: "netty-transport"
    }
    include project(path: ":", configuration: "default")
}

processResources {
    inputs.property "version", project.version

    filesMatching("viaproxy.yml") {
        expand "version": project.version
    }
}

jar {
    dependsOn configurations.include
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.include.collect {
            zipTree(it)
        }
    } {
        exclude "META-INF/*.RSA", "META-INF/*.SF", "META-INF/*.DSA"
    }
}
